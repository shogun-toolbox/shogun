FIND_PACKAGE(R REQUIRED)

UNSET(TARGET_SWIGFLAGS)

include(ShogunInterfaces)
SET(SWIG_CXX_COMPILER_FLAGS "${SWIG_CXX_COMPILER_FLAGS} ${R_CXX_FLAGS}")
IF("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
	SET(SWIG_CXX_COMPILER_FLAGS "${SWIG_CXX_COMPILER_FLAGS} -DDO_NOT_USE_CXX_HEADERS")
ENDIF()

GENERATE_INTERFACE_TARGET(r ${CMAKE_CURRENT_SOURCE_DIR} ${R_LIBRARIES})
set_target_properties(interface_r PROPERTIES PREFIX "")

EXECUTE_PROCESS(COMMAND ${R_EXECUTABLE} --slave -e "with(R.version, cat(sprintf('%s.%s', major, minor)))"
	OUTPUT_VARIABLE R_VERSION
	OUTPUT_STRIP_TRAILING_WHITESPACE
)

EXECUTE_PROCESS(COMMAND ${R_EXECUTABLE} --slave -e "cat(R.version\$platform)"
	OUTPUT_VARIABLE PLATFORM
	OUTPUT_STRIP_TRAILING_WHITESPACE
)

EXECUTE_PROCESS(COMMAND ${R_EXECUTABLE} --slave -e "cat(.Platform\$OS.type)"
	OUTPUT_VARIABLE OSTYPE
	OUTPUT_STRIP_TRAILING_WHITESPACE
)

EXECUTE_PROCESS(COMMAND ${PYTHON_EXECUTABLE} -c "from time import gmtime, strftime; print(strftime(\"%Y-%m-%d %H:%M:%S\", gmtime()))"
	OUTPUT_VARIABLE DATE
	OUTPUT_STRIP_TRAILING_WHITESPACE
)

CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/DESCRIPTION.in ${CMAKE_CURRENT_BINARY_DIR}/DESCRIPTION @ONLY)

ADD_CUSTOM_COMMAND(TARGET interface_r
	POST_BUILD
	COMMAND echo 'wd="${CMAKE_CURRENT_BINARY_DIR}" \; setwd( wd ) \; f="shogun.R" \; fdata="shogun.RData" \; source( f ) \; save( list=ls( all=TRUE ) , file=fdata , compress=TRUE ) \; q( save="no" ) \;' | ${R_EXECUTABLE} --silent --no-save
	WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
	COMMENT "Generating shogun.RData"
)

install(FILES ${CMAKE_CURRENT_BINARY_DIR}/shogun.so
	DESTINATION ${R_COMPONENT_LIB_PATH}/shogun/libs
	COMPONENT r)

install(FILES ${CMAKE_CURRENT_BINARY_DIR}/shogun.RData
	DESTINATION ${R_COMPONENT_LIB_PATH}/shogun/R
	COMPONENT r)

install(FILES ${CMAKE_CURRENT_BINARY_DIR}/DESCRIPTION ${CMAKE_CURRENT_SOURCE_DIR}/NAMESPACE
	DESTINATION ${R_COMPONENT_LIB_PATH}/shogun
	COMPONENT r)

install(CODE "FILE(MAKE_DIRECTORY \$ENV{DESTDIR}/${R_COMPONENT_LIB_PATH}/shogun/Meta)")

install(CODE "EXECUTE_PROCESS(COMMAND ${CMAKE_CURRENT_SOURCE_DIR}/r-install.sh \$ENV{DESTDIR}/${R_COMPONENT_LIB_PATH} shogun so saveRDS WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})")
