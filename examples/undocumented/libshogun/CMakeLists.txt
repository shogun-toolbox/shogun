INCLUDE_DIRECTORIES(${INCLUDES})
if(SYSTEM_INCLUDES)
	INCLUDE_DIRECTORIES(SYSTEM ${SYSTEM_INCLUDES})
endif()

FILE(GLOB EXAMPLES_CPP RELATIVE "${CMAKE_CURRENT_SOURCE_DIR}" "${CMAKE_CURRENT_SOURCE_DIR}/*.cpp")

IF (NOT USE_MOSEK)
	LIST(REMOVE_ITEM EXAMPLES_CPP "so_fg_model.cpp" "so_multiclass.cpp" "so_multilabel.cpp")
ENDIF ()

FOREACH(EXAMPLE_CPP ${EXAMPLES_CPP})
	STRING(REGEX REPLACE ".cpp\$" "" EXAMPLE "${EXAMPLE_CPP}")

	add_executable(${EXAMPLE} ${CMAKE_CURRENT_SOURCE_DIR}/${EXAMPLE_CPP})
	target_link_libraries(${EXAMPLE} shogun ${SANITIZER_LIBRARY})
	IF(SANITIZER_FLAGS)
		set_target_properties(${EXAMPLE} PROPERTIES COMPILE_FLAGS ${SANITIZER_FLAGS})
	ENDIF()
	add_test(libshogun-${EXAMPLE} ${CMAKE_CURRENT_BINARY_DIR}/${EXAMPLE})
	set_property(TEST libshogun-${EXAMPLE} PROPERTY
		WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})
	LIST(APPEND INSTALL_EXAMPLES ${CMAKE_CURRENT_BINARY_DIR}/${EXAMPLE})
ENDFOREACH()

INSTALL(PROGRAMS ${INSTALL_EXAMPLES}
	DESTINATION share/shogun/examples/libshogun
	COMPONENT libshogun-examples)

SET(CPACK_COMPONENTS_ALL ${CPACK_COMPONENTS_ALL} libshogun-examples PARENT_SCOPE)
