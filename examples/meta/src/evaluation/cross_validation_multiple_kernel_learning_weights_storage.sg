File f_feats = csv_file("../../data/fm_train_real.dat")
File f_labels = csv_file("../../data/label_train_twoclass.dat")

#![create_features]
Features feats = features(f_feats)
CombinedFeatures combined_features()
combined_features.append_feature_obj(feats)
combined_features.append_feature_obj(feats)
combined_features.append_feature_obj(feats)
BinaryLabels labels(f_labels)
#![create_features]

#![create_kernels]
Kernel combined_kernel = kernel("CombinedKernel")
Kernel k_1 = kernel("GaussianKernel", log_width=0.1)
Kernel k_2 = kernel("GaussianKernel", log_width=1.0)
Kernel k_3 = kernel("GaussianKernel", log_width=2.0)
combined_kernel.add("kernel_array", k_1)
combined_kernel.add("kernel_array", k_2)
combined_kernel.add("kernel_array", k_3)
#![create_kernels]

#![create_classifier]
Machine libsvm = machine("LibSVM")
Machine svm = machine("MKLClassification", kernel=combined_kernel)
svm.put("interleaved_optimization", False)
svm.put("svm", libsvm)
#![create_classifier]

#![create_cross_validation]
StratifiedCrossValidationSplitting splitting_strategy(labels, 5)
Evaluation evaluation_criterium = evaluation("AccuracyMeasure")
CrossValidation cross(svm, combined_features, labels, splitting_strategy, evaluation_criterium)
cross.set_autolock(False)
cross.set_num_runs(2)
#![create_cross_validation]

#![create_observer]
ParameterObserverCV mkl_obs(True)
cross.subscribe_to_parameters(mkl_obs)
#[!create_observer]

#![evaluate_and_get_result]
EvaluationResult result = cross.evaluate()
#![evaluate_and_get_result]

#![get_results]
real mean = result.get_real("mean")
real stddev = result.get_real("std_dev")
#![get_results]

#![get_fold_machine]
CrossValidationStorage obs = mkl_obs.get_observation(0)
CrossValidationFoldStorage fold = obs.get_fold(0)
Machine machine_mkl = machine(fold.get_trained_machine())

#![get_fold_machine]

#![get_weights]
CombinedKernel k = kernel(machine_mkl.get("kernel"))
RealVector w = k.get_subkernel_weights()
#![get_weights]
