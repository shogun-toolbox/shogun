CSVFile f_feats_train("../../data/regression_1d_sinc_features_train.dat")
CSVFile f_feats_test("../../data/regression_1d_sinc_features_test.dat")
CSVFile f_labels_train("../../data/regression_1d_sinc_labels_train.dat")
CSVFile f_labels_test("../../data/regression_1d_sinc_labels_test.dat")

#![create_features]
Features features_train = features(f_feats_train)
Features features_test = features(f_feats_test)
RegressionLabels labels_train(f_labels_train)
RegressionLabels labels_test(f_labels_test)
#![create_features]

#![create_appropriate_kernel_and_mean_function]
Kernel k = kernel("GaussianKernel", lhs=features_train, rhs=features_train, log_width=0.0)
MeanFunction mean = mean_function("ZeroMean")
#![create_appropriate_kernel_and_mean_function]

#![create_instance]
LikelihoodModel gauss_likelihood = likelihood_model("GaussianLikelihood")
DifferentiableFunction inference_method = differentiable_function("ExactInferenceMethod", kernel=k, features=features_train, mean_function=mean, labels=labels_train, likelihood_model=gauss_likelihood)
Machine gp_regression = machine("GaussianProcessRegression", inference_method=inference_method)
#![create_instance]

#![train_and_apply]
gp_regression.train()
RegressionLabels labels_predict = gp_regression.apply_regression(features_test)
#![train_and_apply]

#![compute_variance]
RealVector variances = labels_predict.get_real_vector("confidence")
#![compute_variance]

#![optimize_marginal_likelihood]
GradientCriterion grad_criterion()
MachineEvaluation grad = machine_evaluation("GradientEvaluation", machine=gp_regression, features=features_train, labels=labels_train, evaluation_criterion=grad_criterion, differentiable_function=inference_method)
GradientModelSelection grad_selection(grad)
ParameterCombination best_theta = grad_selection.select_model()
best_theta.apply_to_machine(gp_regression)
#![optimize_marginal_likelihood]

#![evaluate_error_and_marginal_likelihood]
MeanSquaredError eval()
real mse = eval.evaluate(labels_predict, labels_test)
Inference inf = as_inference(inference_method)
real marg_ll = inf.get_negative_log_marginal_likelihood()
#![evaluate_error_and_marginal_likelihood]

# integration testing variables
RealVector output = labels_test.get_real_vector("labels")
